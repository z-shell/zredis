# Tests for the zdharma/redis module

%prep

 module_path=( `pwd`/Modules )
 modname1="zdharma/db"
 modname2="zdharma/zredis"
 db1="127.0.0.1:6379/10"
 db2="127.0.0.1/11"
 if ! zmodload $modname1 ; then
   VATS_unimplemented="can't load $modname1 module for testing"
 fi
 if ! zmodload $modname2 ; then
   VATS_unimplemented="can't load $modname2 module for testing"
 fi
 redis-cli -n 10 flushdb
 redis-cli -n 11 flushdb

%test

 (zmodload -u $modname2 && zmodload $modname2)
0:unload and reload the module without crashing

 ztie -d db/redis -f $db1 dbase
 dbase[testkey]=testdata
 zuntie dbase
 unset dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store key in database 1
>testdata
>0

 ztie -d db/redis -f $db2 dbase
 dbase[testkey]=testdata
 zuntie dbase
 unset dbase
 ztie -d db/redis -f $db2 dbase
 echo $dbase[testkey]
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store key in database 2
>testdata
>0

 ztie -d db/redis -f $db1 dbase2
 unset 'dbase2[testkey]'
 zuntie dbase2
 ztie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:remove key from database 1 (different variables)
>
>0

 ztie -d db/redis -f $db1 dbase
 dbase[testkey]=testdata
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 unset 'dbase[testkey]'
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store & remove key from database (the same variables)
>testdata
>
>0

 ztie -d db/redis -f $db1 dbase
 dbase[testkey]=testdata
 dbase[testkey2]=$dbase[testkey]
 dbase[testkey3]=$dbase[testkey]x$dbase[testkey2]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 echo $dbase[testkey2]
 echo $dbase[testkey3]
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store 2 keys fetching 1st
>testdata
>testdata
>testdataxtestdata
>0

 ztie -d db/redis -f $db1 dbase
 val=$dbase[testkey2]
 unset 'dbase[testkey2]'
 echo $val
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:unset key that was fetched
>testdata
>0

 ztie -d db/redis -f $db1 dbase
 local -a result
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan tied hash, directly assign local -a
>testdata
>testdataxtestdata
>testkey
>testkey3
>0

 ztie -d db/redis -f $db1 dbase
 dbase=( a a )
 print -rl -- "${(kv)dbase[@]}"
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:Use scan right after overwrite of database
>a
>a
>0

 ztie -d db/redis -f $db1 dbase
 dbase=( a b c d )
 zuntie dbase
 unset dbase
 ztie -d db/redis -f $db1 dbase
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:replace hash / database, scan
>a
>b
>c
>d
>0

 ztie -d db/redis -f $db1 dbase
 local -a arr
 arr=( "${dbase[@]}" )
 print -rl -- "${(o)arr[@]}"
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with no (kv)
>b
>d
>0

 ztie -d db/redis -f $db1 dbase
 result=( "${(k)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with keys only - (k)
>a
>c
>0

 ztie -d db/redis -f $db1 dbase
 result=( "${(v)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with values only, explicit - (v)
>b
>d
>0

 ztie -d db/redis -f ${db1%/*}/1000 dbase 2>/dev/null; ret=$?
 echo $zredis_tied ${#zredis_tied}
 return $ret
1:open non-existent database
>0

 ztie -d db/redis -f $db1 dbase
 dbase=( )
 dbase+=( a b )
 echo $dbase[a]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[a]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 dbase+=( c d )
 echo $dbase[a]
 echo $dbase[c]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[a]
 echo $dbase[c]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zuntie dbase
0:Append with +=( ), also with existing data, also (kv) scan
>b
>b
>a
>b
>b
>d
>a
>b
>c
>d
>b
>d
>a
>b
>c
>d

 ztie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zuntie dbase
0:Type of tied parameter
>association-special

 typeset -ga dbase
 ztie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zuntie dbase
0:Type of tied parameter, with preceding unset
>association-special

 local -a dbase
 ztie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zuntie dbase
0:Type of tied parameter, with local parameter already existing
>association-local-special

 local -a dbase
 dbase=( fromarray )
 () {
     local -a dbase
     ztie -d db/redis -f $db1 dbase
     echo ${(t)dbase}
     zuntie dbase
 }
 echo $dbase[1]
 ztie -d db/redis -f $db1 dbase
 echo "Can connect:" $dbase[a]
 zuntie dbase
0:Test of automatic untie (use of local scope) and of scoping
>association-local-special
>fromarray
>Can connect: b

 echo First $zredis_tied ${#zredis_tied}
 ztie -d db/redis -f $db1 dbase
 echo $zredis_tied ${#zredis_tied}
 ztie -d db/redis -f $db2 dbase2
 echo $zredis_tied ${#zredis_tied}
 zuntie dbase
 echo $zredis_tied ${#zredis_tied}
 zuntie dbase2
 echo $zredis_tied ${#zredis_tied}
0:zredis_tied parameter
>First 0
>dbase 1
>dbase dbase2 2
>dbase2 1
>0

 unset zredis_tied 2>/dev/null
1:unset of read-only zgdbm_tied parameter

 ztie -d db/redis -f $db1 dbase
 dbase[漢字]=漢字
 echo $dbase[漢字]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[漢字]
 zuntie -u dbase
0:Unicode test
>漢字
>漢字

 key="ab"$'\0'"ef"
 ztie -d db/redis -f $db1 dbase
 dbase[$key]=value
 echo $dbase[$key]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[$key]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 dbase[$key]=$key
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 [[ "$dbase[$key]" = "$key" ]] && echo correct
 zuntie dbase
0:Metafication of $'\0'
>value
>value
>correct

 ztie -d db/redis -f $db1 dbase
 dbase=( 漢字 漢字 )
 echo $dbase[漢字]
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 echo $dbase[漢字]
 zuntie dbase
 key="ab"$'\0'"ef"
 ztie -d db/redis -f $db1 dbase
 dbase+=( $key $key )
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 [[ "$dbase[$key]" = "$key" ]] && echo correct
 zuntie dbase
0:Unicode & metafication test, different hash access
>漢字
>漢字
>correct

 ztie -d db/redis -f $db1 dbase
 dbase=( 漢字 漢字 )
 zuntie dbase
 ztie -d db/redis -f $db1 dbase
 noglob print -rl ${(kv)dbase[@]}
 zuntie dbase
0:Hash scanning and metafication
>漢字
>漢字

 ztie -d db/redis -f $db1 dbase
 noglob print -rl ${(okv)dbase[@]}
 zuntie dbase
0:Sorted hash scanning and metafication
>漢字
>漢字

 ztie -d db/redis -f $db1 dbase
 ztaddress dbase
 [[ $REPLY = "127.0.0.1:6379/10" ]] && echo correct
 zuntie dbase
 ztie -d db/redis -f $db2 dbase
 ztaddress dbase
 [[ $REPLY = "127.0.0.1/11" ]] && echo correct
 zuntie dbase
0:ztaddress builtin
>correct
>correct

 ztie -d db/redis -f $db1 dbase
 dbase[testkey]=value1
 fun() { while read line; do echo $line; done }
 { eval "dbase[testkey]=value2" | fun } 2>&1
 echo $dbase[testkey]
 ztclear dbase testkey
 echo $dbase[testkey]
 zuntie dbase
0:Test store in forked Zsh
>(eval):1: Not connected, retrying... Success
>value1
>value2

 ztie -z -d db/redis -f $db1 dbase
 dbase[testkey]=value1
 echo $dbase[testkey]
 fun() { while read line; do echo $line; done }
 { eval "dbase[testkey]=value2" | fun } 2>&1
 echo $dbase[testkey]
 zuntie dbase
0:Test store in forked Zsh, with -z passthrough
>value1
>(eval):1: Not connected, retrying... Success
>value2

 ztie -d db/redis -f $db1 dbase
 echo $dbase[a] $dbase[b] $dbase[c]
 unset dbase
 ztie -r -d db/redis -f $db1 dbase
 echo $dbase[a] $dbase[b] $dbase[c]
 zuntie -u dbase
0:Test references to not-existing elements
>
>

%clean

 redis-cli -n 10 flushdb
 redis-cli -n 11 flushdb
